cmake_minimum_required(VERSION 3.5)

project(libcimpp CXX)
set(CMAKE_CXX_STANDARD 11)

set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_CURRENT_SOURCE_DIR}/cmake/" "${CMAKE_CURRENT_SOURCE_DIR}/../../cmake")

add_subdirectory(doc)
add_subdirectory(thirdparty)

set(libcimpp_MAJOR_VERSION 2)
set(libcimpp_MINOR_VERSION 1)
set(libcimpp_PATCH_VERSION 0)
set(libcimpp_VERSION ${libcimpp_MAJOR_VERSION}.${libcimpp_MINOR_VERSION}.${libcimpp_PATCH_VERSION})

# Options
option(BUILD_SHARED_LIBS "Build shared library" OFF)

if(NOT USE_CIM_VERSION)
	set(USE_CIM_VERSION "CGMES_2.4.15_27JAN2020" CACHE STRING "Define CIM Version")
endif()

message(STATUS "Building ${PROJECT_NAME} with USE_CIM_VERSION=${USE_CIM_VERSION}")

if(USE_CIM_VERSION STREQUAL "CGMES_2.4.15_16FEB2016")
  set(CIM_FOLDER ${USE_CIM_VERSION})
  set(CGMES_FOLDER "${CMAKE_CURRENT_SOURCE_DIR}/CGMES_2.4.15/generate")
  set(CIM_NAME "CGMES 2_4_15 16_02_2016")
elseif(USE_CIM_VERSION STREQUAL "CGMES_2.4.15_27JAN2020")
  set(CIM_FOLDER ${USE_CIM_VERSION})
  set(CGMES_FOLDER "${CMAKE_CURRENT_SOURCE_DIR}/CGMES_2.4.15/generate")
  set(CIM_NAME "CGMES 2_4_15 27_01_2020")
elseif(USE_CIM_VERSION STREQUAL "IEC61970_17v07")
  set(CIM_FOLDER "17v07")
  set(CIM_NAME "IEC61970 17v07")
elseif(USE_CIM_VERSION STREQUAL "IEC61970_17v23")
  set(CIM_FOLDER "17v23")
  set(CIM_NAME "IEC61970 17v23")
elseif(USE_CIM_VERSION STREQUAL "IEC61970_17v23_IEC61968_13v11")
  set(CIM_FOLDER "17v23_13v11")
  set(CIM_NAME "IEC61970 17v23 - IEC61968 13v11")
elseif(USE_CIM_VERSION STREQUAL "IEC61970_16v29a")
  set(CIM_FOLDER "16v29a")
  set(CIM_NAME "IEC61970 16v29a")
elseif(USE_CIM_VERSION STREQUAL "IEC61970_16v29a_IEC61968_12v08")
  set(CIM_FOLDER "16v29a_12v08")
  set(CIM_NAME "IEC61970 16v29a - IEC61968 12v08")
elseif(USE_CIM_VERSION STREQUAL "IEC61970_16v29a_SINERGIEN")
  file(GLOB SIN_DIR_PATH_FILE ${CMAKE_CURRENT_SOURCE_DIR}/16v29a_SINERGIEN*/BaseClass.h)
  string(REGEX MATCH "16v29a_SINERGIEN_([^/])*" SIN_DIR_PATH "${SIN_DIR_PATH_FILE}")
  set(CIM_FOLDER ${SIN_DIR_PATH})
  set(CIM_NAME "16v29a SINERGIEN")
else()
  message(FATAL_ERROR "${USE_CIM_VERSION} is an invalid value for USE_CIM_VERSION")
endif()

if(CGMES_FOLDER)
  set(SCHEMA_FOLDER "${CGMES_FOLDER}/cgmes_schema/${USE_CIM_VERSION}")
  execute_process(COMMAND mkdir -p ${CMAKE_CURRENT_SOURCE_DIR}/${CIM_FOLDER}/IEC61970/)
  file(GLOB STATIC_SRCS ${CMAKE_CURRENT_SOURCE_DIR}/CGMES_2.4.15/static/*.cpp ${CMAKE_CURRENT_SOURCE_DIR}/CGMES_2.4.15/static/*.hpp ${CMAKE_CURRENT_SOURCE_DIR}/CGMES_2.4.15/static/*.h)
  foreach(static_file ${STATIC_SRCS})
    file(COPY ${static_file} DESTINATION ${CMAKE_CURRENT_SOURCE_DIR}/${CIM_FOLDER})
  endforeach()
  file(COPY ${CMAKE_CURRENT_SOURCE_DIR}/CGMES_2.4.15/static/IEC61970/IEC61970CIMVersion.h DESTINATION ${CMAKE_CURRENT_SOURCE_DIR}/${CIM_FOLDER}/IEC61970)
  file(COPY ${CMAKE_CURRENT_SOURCE_DIR}/CGMES_2.4.15/static/IEC61970/IEC61970CIMVersion.cpp DESTINATION ${CMAKE_CURRENT_SOURCE_DIR}/${CIM_FOLDER}/IEC61970)
  find_package (Python COMPONENTS Interpreter)
  execute_process(COMMAND ${Python_EXECUTABLE} -m pip install xmltodict chevron)
  execute_process(WORKING_DIRECTORY ${CGMES_FOLDER} COMMAND ${Python_EXECUTABLE} ${CGMES_FOLDER}/build.py ${CMAKE_CURRENT_SOURCE_DIR}/${CIM_FOLDER} ${SCHEMA_FOLDER} cpp)
  unset(SCHEMA_FOLDER)
endif()

file(GLOB_RECURSE SRC src/*.cpp)
file(GLOB_RECURSE CIM_SRC ${CIM_FOLDER}/*.cpp)
add_library(${PROJECT_NAME} ${SRC} ${CIM_SRC})
set_target_properties(${PROJECT_NAME} PROPERTIES OUTPUT_NAME "cimpp${CIM_FOLDER}")
target_include_directories(${PROJECT_NAME} PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/${CIM_FOLDER}>)
target_include_directories(${PROJECT_NAME} PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/src>)
target_link_libraries(${PROJECT_NAME} PUBLIC arabica)
set_property(TARGET arabica PROPERTY POSITION_INDEPENDENT_CODE ON)

include(GNUInstallDirs)

install(TARGETS ${PROJECT_NAME}
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}/static
)

file(GLOB HEADERS "src/*.h" "src/*.hpp")
install(FILES ${HEADERS}
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/cimpp/${CIM_FOLDER}
    COMPONENT devel
)

install(DIRECTORY ${CIM_FOLDER}
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/cimpp
    COMPONENT devel
    FILES_MATCHING
        PATTERN "*.h"
        PATTERN "*.hpp"
)

# Settings for packaging
include(CIMppPackaging)

# Show feature summary
include(FeatureSummary)
feature_summary(WHAT ALL VAR enabledFeaturesText)
message(STATUS "${enabledFeaturesText}")
message(STATUS "Building CIMpp with USE_CIM_VERSION=${USE_CIM_VERSION}")
